## texk/bibtex-x/ac/bibtex-x.ac: configure.ac fragment for the TeX Live subdirectory texk/bibtex-x/
dnl
dnl Copyright (C) 2013 Peter Breitenlohner <tex-live@tug.org>
dnl You may freely use, modify and/or distribute this file.
dnl
## configure options for bibtex-x
m4_define_default([kpse_indent_26], [26])[]dnl
m4_define([kpse_bibtex_x_progs], [bibtex8 bibtexu])[]dnl
AC_FOREACH([Kpse_Prog], kpse_bibtex_x_progs, [dnl
AC_ARG_ENABLE(Kpse_Prog,
              AS_HELP_STRING([--disable-]Kpse_Prog,
                             [do not build the ]Kpse_Prog[ program],
                             kpse_indent_26))
AS_CASE([$enable_]Kpse_Prog,
        [yes | no], ,
        [enable_]Kpse_Prog[=yes])
])
test "x$enable_bibtex_x:$enable_bibtexu" = xyes:yes && need_icu=yes
